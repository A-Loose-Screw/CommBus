import static org.apache.tools.ant.taskdefs.condition.Os.*

apply plugin: 'cpp-application'

application {
  dependencies {
    implementation project(":CommBus")
  }

  source.from file("src/cpp")
  privateHeaders.from file("src/include")

  binaries.configureEach {
    if (toolChain instanceof Gcc && targetMachine.operatingSystemFamily.linux) {
      linkTask.get().linkerArgs.add("-lpthread")
    }

    if (toolChain instanceof Gcc) {
      compileTask.get().compilerArgs = ["-std=c++17"]
    }

    if (project.hasProperty('test')) {
      println("CommBus Test Mode")
      compileTask.get().macros.put("COMMBUS_TEST", null)
    }
  }
}

task sim(type:Exec) {
  dependsOn(build)
  description "Simulate ${project.name}"
  workingDir "${project.buildDir}/exe/${application.name}/debug/"

  if (isFamily(FAMILY_UNIX) || isFamily(FAMILY_MAC)) {
    commandLine "./${project.name}"
  } else if (isFamily(FAMILY_WINDOWS)) {
    commandLine "cmd", "/c", "${project.name}.exe"
  } else {
    ant.fail("Could not determin os family")
  }
}